
# CMAKE_BINARY_DIR变量指向cmake build tree的顶级目录。
if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt")
endif()

# 从install_manifest.txt中读取文本内容，并根据\n拆分成一个文件列表
file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")

# 循环删除列表中的文件
foreach(file ${files})
  message(STATUS "Uninstalling $ENV{DESTDIR}${file}")

  if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")

    # 调用cmake -E remove <文件名>来删除文件
    # 其中$ENV{DESTDIR}当make DESTDIR=<安装目录> install
    # 在命令行指定安装目录的将被设置，但是install_manifest.txt中
    # 的文件路径不包括DESTDIR指定的部分
    exec_program(
      "@CMAKE_COMMAND@" ARGS "-E remove \"$ENV{DESTDIR}${file}\""
      OUTPUT_VARIABLE rm_out
      RETURN_VALUE rm_retval
      )

    if(NOT "${rm_retval}" STREQUAL 0)
      message(FATAL_ERROR "Problem when removing $ENV{DESTDIR}${file}")
    endif()

  else(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
  endif()
endforeach()

